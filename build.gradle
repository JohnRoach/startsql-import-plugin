buildscript {
  repositories {
      jcenter()
  }
  dependencies {
      classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
  }
}

import org.gradle.internal.jvm.Jvm;
import org.gradle.util.GradleVersion;

apply plugin: 'groovy'
apply plugin: 'eclipse'

repositories {
  mavenLocal()
  mavenCentral()
}
dependencies {
  compile(
    gradleApi(),
    localGroovy()
  )
  testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude group: 'org.codehaus.groovy'
  }
}

//Set the default build and version properties
project.group='com.spindrift.atg'
project.ext {
  timestamp = new Date()
  buildUser=System.getProperty('user.name')
  buildNumber="(development build by $buildUser)"
}
if (!hasProperty('release') || (hasProperty('release') && release!='true')) {
  project.version+='-SNAPSHOT'
}
project.logger.quiet "Plugin ${project.name} version: ${project.version}"
project.logger.quiet "BuildNumber: ${buildNumber}"
project.logger.quiet("Gradle Version: " +GradleVersion.current().getVersion())
project.logger.quiet("Java Version: " +Jvm.current())

//Set java compatibilities
targetCompatibility = sourceCompatibility = "1.6"

//Apply additional functionality
apply from: 'gradle/maven.gradle'

//Set the default task when none supplied:
defaultTasks 'install'

//For integration testing
task integrationTest(type: GradleBuild) {
  description="Runs startSQLImport integration test"
  group="Verification"
  dir = 'integration-test'
  tasks = ['tasks','startSQLImport']
}

// Add the wrapper task to enable auto-install of the configured version via ./gradlew
task wrapper(type: Wrapper) {
  gradleVersion = "2.0"
}

//Publishing to Bintray
apply plugin: 'nu.studer.plugindev'

plugindev {
  pluginImplementationClass 'com.spindrift.gradle.atg.StartSQLImportPlugin'
  pluginDescription 'Gradle plugin that wraps Oracle Commerce (ATG) startSQLImport utility to import XML into a versioned repository.'
  pluginLicenses 'Apache-2.0'
  pluginTags 'gradle', 'plugin', 'spindrift', 'atg', 'startsqlimport'
  authorId 'spindrift'
  authorName 'Warren Muller'
  authorEmail 'gradle@spindriftgroup.com'
  projectUrl 'https://github.com/spindriftgroup/startsql-import-plugin.git'
  projectInceptionYear '2014'
  done() // do not omit this
}

bintray {
  user = "$BINTRAY_USER"
  key = "$BINTRAY_API_KEY"
  pkg.repo = 'gradle-plugins'
}

